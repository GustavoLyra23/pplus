plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.1.20'
    id 'antlr'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'edu.sc.seis.launch4j' version '2.5.0'
}

group = 'org.gustavolyra'
version = '0.1.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
    antlr 'org.antlr:antlr4:4.13.1'
    implementation 'org.antlr:antlr4-runtime:4.13.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ['-visitor', '-package', 'org.gustavolyra.portugolpp']
    outputDirectory = file("${layout.buildDirectory.get()}/generated-src/antlr/main")
}

sourceSets {
    main {
        java {
            srcDir "${layout.buildDirectory.get()}/generated-src/antlr/main"
        }
    }
}

compileKotlin {
    dependsOn generateGrammarSource
}

compileTestKotlin {
    dependsOn generateTestGrammarSource
}

clean {
    delete "${layout.buildDirectory.get()}/generated-src"
}

test {
    useJUnitPlatform()
}

kotlin {
    jvmToolchain(21)
}

shadowJar {
    archiveBaseName.set('portugolpp')
    archiveClassifier.set('')
    archiveVersion.set('')
    manifest {
        attributes 'Main-Class': 'MainKt'
    }
}

launch4j {
    jarTask = tasks.shadowJar
    copyConfigurable = []
    mainClassName = 'MainKt'
    outfile = 'portugolpp.exe'
    headerType = 'console'
    jreMinVersion = '21'
}

tasks.named('launch4j') {
    dependsOn shadowJar
}