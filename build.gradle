plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.1.20'
    id 'antlr'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'org.gustavolyra'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    antlr 'org.antlr:antlr4:4.13.1'
    implementation 'org.antlr:antlr4-runtime:4.13.1'
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3")

}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ['-visitor', '-package', 'org.gustavolyra.portugolpp']
    outputDirectory = file("$buildDir/generated-src/antlr/main")
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated-src/antlr/main"
        }
    }
}

compileKotlin {
    dependsOn generateGrammarSource
}

clean {
    delete "$buildDir/generated-src"
}

test {
    useJUnitPlatform()
}

kotlin {
    jvmToolchain(21)
}

shadowJar {
    manifest {
        attributes 'Main-Class': 'MainKt'
    }
    archiveBaseName.set('portugolpp')
    archiveClassifier.set('')
    archiveVersion.set('')
}